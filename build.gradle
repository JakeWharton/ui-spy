buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10'
		classpath 'org.jetbrains.kotlin:kotlin-serialization:1.6.10'
	}
}

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'application'

mainClassName = 'com.jakewharton.uispy.Main'

dependencies {
	def okhttpVersion = '4.9.3'
	def kotlinxSerializationVersion = '1.3.2'

	implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0'
	implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:$kotlinxSerializationVersion"
	implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinxSerializationVersion"
	implementation 'com.akuleshov7:ktoml-core:0.2.11'
	implementation 'org.jetbrains.kotlinx:kotlinx-datetime:0.3.2'
	implementation 'com.github.ajalt.clikt:clikt:3.4.0'
	implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
	implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"

	testImplementation 'junit:junit:4.13.2'
	testImplementation 'com.google.truth:truth:1.1.3'
	testImplementation 'com.google.jimfs:jimfs:1.2'
	testImplementation "com.squareup.okhttp3:mockwebserver:$okhttpVersion"
	testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

repositories {
	mavenCentral()
}

tasks.named("distTar").configure { task ->
	task.enabled = false
}
tasks.named("assemble").configure { task ->
	task.dependsOn(tasks.named("installDist"))
}

tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach { task ->
	task.kotlinOptions {
		jvmTarget = '1.8'
		freeCompilerArgs += [
				'-progressive',
				'-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
		]
	}
}
